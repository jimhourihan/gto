//
// Copyright (C) 2003 Tweak Films
//
// This library is free software; you can redistribute it and/or
// modify it under the terms of the GNU Lesser General Public License
// as published by the Free Software Foundation; either version 2 of
// the License, or (at your option) any later version.
//
// This library is distributed in the hope that it will be useful, but
// WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
// Lesser General Public License for more details.
//
// You should have received a copy of the GNU Lesser General Public
// License along with this library; if not, write to the Free Software
// Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA 02111-1307
// USA
//


global proc string currentGtoExportSet()
{
    string $selectedNodes[] =  `textScrollList -q -si GtoSetOutliner`;
    return $selectedNodes[0];
}

// *********************************
proc string[] getExportSetList()
{
    string $allSets[];
    clear $allSets;
    $allSets = `ls -et objectSet`;
    
    string $sets[];
    clear $sets;
    int $j = 0;
    for( $i = 0; $i < size( $allSets ); $i++ )
    {
        if( ! ( $allSets[$i] == "defaultLightSet"
            || $allSets[$i] == "defaultObjectSet"
            || $allSets[$i] == "initialParticleSE"
            || $allSets[$i] == "initialShadingGroup"
            ) )
        {
            $sets[$j] = $allSets[$i];
            $j++;
        }
    }
    sort( $sets );
    return $sets;
}

// *********************************
global proc int GtoExportSetFileBrowserActionCallback( string $textFieldName,
                                                       string $file, 
                                                       string $type )
{
    textField -edit -text $file $textFieldName;
    
    // Return true to close the filebrowser.
    return 1;
}


// *********************************
global proc GtoExportSetFileBrowserButtonCallback( string $textFieldName )
{
    global string $gDefaultFileBrowserDir;

    $existingValue = `textField -q -text $textFieldName`;
//     if( `filetest -r $existingValue` )
//     {
//         $gDefaultFileBrowserDir = $existingValue;
//         workspace -dir $existingValue;
//     }
//     else
//     {
//         $gDefaultFileBrowserDir = "";
//     }

    string $callback = "GtoExportSetFileBrowserActionCallback(\"" + $textFieldName + "\")";

    fileBrowser $callback "Set" "GTO" 1;
}


// *********************************
proc buildFilenameBox( string $defaultFilename, string $parent )
{
    string $prevParent = `setParent -q`;
    setParent $parent;
    columnLayout;
    text -label "";
    text -label "";
    rowLayout -nc 3 -adj 2 -ct1 "left" -co1 10;
    text -l "Filename:" GtoExportFilenameLabel;
    string $textFieldName = ( $parent + "Filename" );
    print $textFieldName;
    textField -w 300 -text $defaultFilename $textFieldName;
    symbolButton -image "navButtonBrowse.xpm" 
                 -command ("GtoExportSetFileBrowserButtonCallback(\"" + $textFieldName + "\")" )
                 GtoExportBrowserButton;

    setParent $prevParent;    
}

// *********************************
global proc makeGtoSetCallback()
{
    string $selectedNodes[] =  `textScrollList -q -si GtoAllSetsOutliner`;
    string $selectedNode = $selectedNodes[0];
    
    if( ! attributeExists( "GTO_ExportOptions", $selectedNode ) )
    {
        addAttr -ln "GTO_ExportOptions" -dt "string" $selectedNode;
    }
    string $attrName = $selectedNode + ".GTO_ExportOptions";
    setAttr -type "string" $attrName `optionVar -q GTOOptions`;
}


// *********************************
global proc GtoExportRefreshSetList()
{
    textScrollList -e -ra GtoSetOutliner;
    textScrollList -e -ra GtoAllSetsOutliner;
    string $sets[] = getExportSetList();
    for( $i = 0; $i < size( $sets ); $i++ )
    {
        if( attributeExists( "GTO_ExportOptions", $sets[$i] ) )
        {
            textScrollList -edit -append $sets[$i] GtoSetOutliner;
        }
        else
        {
            textScrollList -edit -append $sets[$i] GtoAllSetsOutliner;
        }
    }
}

// *********************************
proc buildSetList()
{
    tabLayout -cr true 
              -cc "gtoExportMakeRightPane"
              GtoSetOutlinerTabs;
    
    $tab1 = `frameLayout -lv false -bv false`;
    textScrollList -allowMultiSelection false
                   -selectCommand "buildGtoOptions"
                   -font boldLabelFont
                   GtoSetOutliner;
    setParent ..;

    $tab2 = `frameLayout -lv false -bv false`;
    textScrollList -allowMultiSelection false
                   -selectCommand "makeGtoSetCallback"
                   -font boldLabelFont
                   GtoAllSetsOutliner;
    setParent ..;

    tabLayout -edit
        -tabLabel $tab1 "GTO Sets" 
        -tabLabel $tab2 "All Sets"
        GtoSetOutlinerTabs;

    setParent ..; 
}

// *********************************
global proc GtoSaveForSetCallback()
{
    gtoExportOptions( "gtoOptionsLayout", 
                      "query", 
                      "",
                      "gtoOptionsSetCallback" );
}

// *********************************
global proc GtoSaveAndCloseCallback()
{
//    GtoSaveForSetCallback();
    deleteUI gtoExportSetsWindow;
}

// *********************************
global proc gtoOptionsSetCallback( string $optionsString )
{
    if( size( currentGtoExportSet() ) == 0 )
    {
        print "ERROR: What set is this for?\n";
        return;
    }

    if( ! attributeExists( "GTO_ExportOptions", currentGtoExportSet() ) )
    {
        addAttr -ln "GTO_ExportOptions" -dt "string" `currentGtoExportSet`;
    }
    string $attrName = currentGtoExportSet() + ".GTO_ExportOptions";
    setAttr -type "string" $attrName $optionsString;

    if( ! attributeExists( "GTO_RefFilename", currentGtoExportSet() ) )
    {
        addAttr -ln "GTO_RefFilename" -dt "string" `currentGtoExportSet`;
    }
    $attrName = currentGtoExportSet() + ".GTO_RefFilename";
    setAttr -type "string" $attrName `textField -q -text gtoExportRefFilename`;

    if( ! attributeExists( "GTO_DiffFilename", currentGtoExportSet() ) )
    {
        addAttr -ln "GTO_DiffFilename" -dt "string" `currentGtoExportSet`;
    }
    $attrName = currentGtoExportSet() + ".GTO_DiffFilename";
    setAttr -type "string" $attrName `textField -q -text gtoExportDiffFilename`;
}


// *********************************
global proc GtoExportDoRefHelper( string $optionsString )
{
    // Make options reference-only
    $optionsString = `substitute "anim=1" $optionsString "anim=0"`;
    $optionsString = `substitute "isdifference=1" $optionsString "isdifference=0"`;
    $optionsString = `substitute "diffpositions=1" $optionsString "diffpositions=0"`;
    $optionsString = `substitute "diffnormals=1" $optionsString "diffnormals=0"`;
    $optionsString = `substitute "diffmatrices=1" $optionsString "diffmatrices=0"`;
    
    string $filename = `textField -q -text gtoExportRefFilename`;
    
    string $nodes[];
    $nodes = `selectedNodes`;
    
    select -r `currentGtoExportSet`;

    string $dirname = dirname( $filename );
    sysFile -md $dirname;
    
    file -force -type "GTO" -options $optionsString -es $filename;
    
    if( size( $nodes ) == 0 )
    {
        select -d;
    }
    else
    {
        select -r $nodes;
    }
}


// *********************************
global proc GtoExportDoRefCallback()
{
    gtoExportOptions( "gtoOptionsLayout", 
                      "query", 
                      "",
                      "GtoExportDoRefHelper" );
}

// *********************************
global proc GtoExportDoDiffHelper( string $optionsString )
{
    // Make options reference-only
    $optionsString = `substitute "anim=0" $optionsString "anim=1"`;
    $optionsString = `substitute "isdifference=0" $optionsString "isdifference=1"`;
    
    string $filename = `textField -q -text gtoExportDiffFilename`;
    
    string $nodes[];
    $nodes = `selectedNodes`;
    
    select -r `currentGtoExportSet`;

    string $dirname = dirname( $filename );
    sysFile -md $dirname;
    
    file -force -type "GTO" -options $optionsString -es $filename;
    
    if( size( $nodes ) == 0 )
    {
        select -d;
    }
    else
    {
        select -r $nodes;
    }
}

// *********************************
global proc GtoExportDoDiffCallback()
{
    gtoExportOptions( "gtoOptionsLayout", 
                      "query", 
                      "",
                      "GtoExportDoDiffHelper" );
}


// *********************************
global proc buildGtoOptions()
{
    if( `columnLayout -q -exists gtoOptionsColLayout` )
    {
        deleteUI gtoOptionsColLayout;
    }
    setParent GtoExportOptionsParent;
    
    columnLayout -adjustableColumn true gtoOptionsColLayout;

    string $selectedNodes[] = `textScrollList -q -si GtoSetOutliner`;

    if( size( $selectedNodes ) != 1 )
    {
        text -label "Weird error: Either no sets, or more than one set is selected";
        return;
    }
    string $selectedNode = $selectedNodes[0];
    string $defaultOptions = `optionVar -q GTOOptions`;
    string $projDir = `getenv "SHOT"`;
    string $defaultRefFilename = $projDir + "/gto/" + $selectedNode + "/" 
                                 + $selectedNode + ".ref.gto";
    string $defaultDiffFilename = $projDir + "/gto/cache/" + $selectedNode + "/"
                                 + $selectedNode + ".#.gto";

    if( attributeExists( "GTO_ExportOptions", $selectedNode ) )
    {
        $defaultOptions = 
                    `getAttr ( $selectedNode + ".GTO_ExportOptions" )`;
    }
    if( attributeExists( "GTO_RefFilename", $selectedNode ) )
    {
        $defaultRefFilename = 
                    `getAttr ( $selectedNode + ".GTO_RefFilename" )`;
    }
    if( attributeExists( "GTO_DiffFilename", $selectedNode ) )
    {
        $defaultDiffFilename = 
                    `getAttr ( $selectedNode + ".GTO_DiffFilename" )`;
    }

    textField -edit -text $defaultRefFilename gtoExportRefFilename;
    textField -edit -text $defaultDiffFilename gtoExportDiffFilename;

    columnLayout gtoOptionsLayout;
    gtoExportOptions( "gtoOptionsLayout", 
                      "post", 
                      $defaultOptions,
                      "gtoOptionsSetCallback" );
}

global proc gtoExportMakeRightPane()
{
    setParent GtoExportSetRightPane;
    GtoExportRefreshSetList();
    
    string $uiChildren[] = `formLayout -q -ca GtoExportSetRightPane`;
    for( $i = 0; $i < size( $uiChildren ); $i++ )
    {
        deleteUI $uiChildren[$i];
    }
    
    if( `tabLayout -q -sti GtoSetOutlinerTabs` == 1 )
    {
        if( `textScrollList -q -numberOfItems GtoSetOutliner` == 0 )
        {
            setParent GtoExportSetRightPane;
            return;
        }
        textScrollList -e -sii 1 GtoSetOutliner;
        text -l "Reference Filename:" GtoExportFilenameLabelRef;
        textField -w 300 gtoExportRefFilename;
        symbolButton -image "navButtonBrowse.xpm" 
                         -command ("GtoExportSetFileBrowserButtonCallback(\"gtoExportRefFilename\")" )
                         GtoExportBrowserButtonRef;
        text -l "Difference Filename:" GtoExportFilenameLabelDiff;
        textField -w 300 gtoExportDiffFilename;
        symbolButton -image "navButtonBrowse.xpm" 
                     -command ("GtoExportSetFileBrowserButtonCallback(\"gtoExportDiffFilename\")" )
                     GtoExportBrowserButtonDiff;
        scrollLayout GtoExportOptionsParent;
        buildGtoOptions();
        setParent GtoExportSetRightPane;
        $b1 = `button -align "center" -label "Save Settings" -c "GtoSaveForSetCallback"`;
        $b2 = `button -align "center" -label "Close" -c "GtoSaveAndCloseCallback"`;
        $b3 = `button -align "center" -label "Export Reference" -c "GtoExportDoRefCallback"`;
        $b4 = `button -align "center" -label "Export Animation" -c "GtoExportDoDiffCallback"`;
        formLayout -edit
            -attachForm GtoExportFilenameLabelRef "top" 10
            -attachForm GtoExportFilenameLabelRef "left" 10

            -attachControl gtoExportRefFilename "left" 4 GtoExportFilenameLabelRef
            -attachControl gtoExportRefFilename "right" 4 GtoExportBrowserButtonRef
            -attachForm gtoExportRefFilename "top" 10

            -attachForm GtoExportBrowserButtonRef "top" 10
            -attachPosition GtoExportBrowserButtonRef "left" 10 90
            -attachForm GtoExportBrowserButtonRef "right" 10 

            -attachControl GtoExportFilenameLabelDiff "top" 8 GtoExportFilenameLabelRef
            -attachForm GtoExportFilenameLabelDiff "left" 10

            -attachControl gtoExportDiffFilename "left" 1 GtoExportFilenameLabelDiff
            -attachControl gtoExportDiffFilename "right" 1 GtoExportBrowserButtonDiff
            -attachControl gtoExportDiffFilename "top" 2 gtoExportRefFilename

            -attachControl GtoExportBrowserButtonDiff "top" 2 GtoExportBrowserButtonRef
            -attachPosition GtoExportBrowserButtonDiff "left" 10 90
            -attachForm GtoExportBrowserButtonDiff "right" 10 


            -attachControl GtoExportOptionsParent "top" 10 gtoExportDiffFilename
            -attachForm GtoExportOptionsParent "left" 10
            -attachForm GtoExportOptionsParent "right" 10
            -attachPosition GtoExportOptionsParent "bottom" 10 90

            -attachControl $b3 "top" 10 GtoExportOptionsParent
            -attachForm $b3 "left" 5
            -attachPosition $b3 "right" 1 50
            -attachControl $b4 "top" 10 GtoExportOptionsParent
            -attachForm $b4 "right" 5
            -attachPosition $b4 "left" 1 50

            -attachControl $b1 "top" 5 $b3
            -attachForm $b1 "bottom" 5
            -attachForm $b1 "left" 5
            -attachPosition $b1 "right" 1 50
            -attachForm $b2 "bottom" 5
            -attachControl $b2 "top" 5 $b4
            -attachForm $b2 "right" 5
            -attachPosition $b2 "left" 1 50

//             -attachControl $b3 "bottom" 5 $b1
//             -attachForm $b3 "left" 5
//             -attachPosition $b3 "right" 1 50
//             -attachControl $b4 "bottom" 5 $b2
//             -attachForm $b4 "right" 5
//             -attachPosition $b4 "left" 1 50
// 
//             -attachForm $b1 "bottom" 10
//             -attachForm $b1 "left" 5
//             -attachPosition $b1 "right" 1 50
//             -attachForm $b2 "bottom" 10
//             -attachForm $b2 "right" 5
//             -attachPosition $b2 "left" 1 50
            GtoExportSetRightPane;
    }
    else
    {
        string $pluginInfo = `pluginInfo -q -n gtoIO`
                             + "    © " + `pluginInfo -q -vd gtoIO`;;
        $label1 = `text -label $pluginInfo`;
        $pluginInfo = `pluginInfo -q -v gtoIO`;
        $label2 = `text -label $pluginInfo`;
        $b1 = `button -label "  Make GTO Export Set  " 
                      -command "makeGtoSetCallback"`;
        text -label " ";
        $b2 = `button -label "  Close  " 
                      -command "deleteUI gtoExportSetsWindow"`;

        formLayout -edit
            -attachForm $label1 "left" 60
            -attachPosition $label1 "top" 1 10 
            -attachForm $label2 "left" 60
            -attachControl $label2 "top" 1 $label1
            
            -attachForm $b1 "left" 30
            -attachForm $b1 "right" 30
            -attachPosition $b1 "top" 1 30
            
            -attachForm $b2 "left" 30
            -attachForm $b2 "right" 30
            -attachPosition $b2 "top" 10 90
            GtoExportSetRightPane;
    
        if( size( getExportSetList() ) == 0 )
        {
            button -edit -enable false $b1;
        }
    }
}

// *********************************
global proc gtoExportSet()
{
    int       $bResult;
    string    $currentOptions;
    string    $optionList[];
    string    $optionBreakDown[];
    int       $index;

    if( `window -q -ex gtoExportSetsWindow` )
    {
        deleteUI -window gtoExportSetsWindow;
    }

    window -title "GTO Export Sets" gtoExportSetsWindow;

    frameLayout -lv false gto_leftRightForm;
        paneLayout
            -configuration "vertical2"
            -separatorThickness 5
            -paneSize 1 30 100
            gto_leftRightLayout;
        
        buildSetList();
        
        formLayout -nd 100 GtoExportSetRightPane;
        gtoExportMakeRightPane();
        if( `textScrollList -q -numberOfItems GtoSetOutliner` == 0 )
        {
            tabLayout -edit -sti 2 GtoSetOutlinerTabs;
            gtoExportMakeRightPane();
        }
        else
        {
            tabLayout -edit -sti 1 GtoSetOutlinerTabs;
        }

        setParent ..;
    setParent ..;

    showWindow gtoExportSetsWindow;
}
